shape main
{
	layouttype="border";
	noshadow=true;
	
	if(hasproperty("rectanglenotation","0"))
	{
		// Иконочная нотация
		defsize(90,45);
		startpath(); // Булка
		moveto(8, 100);
		arcto(0,0, 100,125, 100,100, 0,100);
		endpath();
		fillandstrokepath();
		moveto(25, 8);	// Царапки
		lineto(25, 30);
		moveto(50, 0);
		lineto(50, 30);
		moveto(75, 8);
		lineto(75, 30);
	}
	else
	{
		// Прямоугольная нотация
		defsize(100,70);
		startpath(); // Восьмиугольник
		moveto(10,0);
		lineto(90,0);
		lineto(100,20);
		lineto(100,80);
		lineto(90,100);
		lineto(10,100);
		lineto(0,80);
		lineto(0,20);
		lineto(10,0);
		endpath();
		fillandstrokepath();
		
		addsubshape("padding","n"); // Создание отступа для декорации
		addsubshape("name","center");
		
		shape padding
		{
			preferredheight=20;
		}
		
		shape name
		{
			h_align="center";
			print("#name#");
		}
	}
}

shape label
{
	setorigin("sw",0,0);

	if(hasproperty("rectanglenotation","0"))
	{
		print("#name#");
	}
}

decoration need
{
	orientation="ne";
	setorigin("ne", -20,0); // Смещение декорации для восьмиугольника
	
	if(hasproperty("rectanglenotation","0"))
	{
	}
	else
	{
		// Декарация «Потребность» не является полной копией
		startpath(); // Булка
		moveto(8, 100);
		arcto(0,0, 100,125, 100,100, 0,100);
		endpath();
		fillandstrokepath();
		moveto(25, 8);	// Царапки
		lineto(25, 30);
		moveto(50, 0);
		lineto(50, 30);
		moveto(75, 8);
		lineto(75, 30);
	}
}

decoration composite
{
	orientation="se";
	if(hasproperty("iscomposite","true"))
	{
		// Три звена цепочки
		ellipse(0,40, 40,60);
		ellipse(60,40, 100,60);
		moveto(30,50);
		lineto(70,50);
	}
}