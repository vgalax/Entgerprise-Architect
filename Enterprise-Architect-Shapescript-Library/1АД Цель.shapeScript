shape main
{
	layouttype="border";
	noshadow=true;
	
	if(hasproperty("rectanglenotation","0"))
	{
		// Иконочная нотация
		defsize(50,50);
		ellipse(0,0, 100,100); // Окружности
		ellipse(15,15, 85,85);
		ellipse(30,30, 70,70);
		moveto(50,0); // Прицел — вертикаль верхняя
		lineto(50,40);
		moveto(50,60); // Прицел — вертикаль нижняя
		lineto(50,100);
		moveto(0,50); // Прицел — горизонталь левая
		lineto(40,50);
		moveto(60,50); // Прицел — горизонталь правая
		lineto(100,50);
		if(hastag("Погружение","Цель сопутствующая"))
		{
			ellipse(50,50, 100,100);
			ellipse(65,65, 85,85);	
		}
	}
	else
	{
		// Прямоугольная нотация
		defsize(100,70);
		startpath(); // Восьмиугольник
		moveto(10,0);
		lineto(90,0);
		lineto(100,20);
		lineto(100,80);
		lineto(90,100);
		lineto(10,100);
		lineto(0,80);
		lineto(0,20);
		lineto(10,0);
		endpath();
		fillandstrokepath();
		
		addsubshape("padding","n"); // Создание отступа для декорации
		addsubshape("name","center");
		
		shape padding
		{
			preferredheight=20;
		}
		
		shape name
		{
			h_align="center";
			print("#name#");
		}
	}
}

shape label
{
	setorigin("sw",0,0);

	if(hasproperty("rectanglenotation","0"))
	{
		print("#name#");
	}
}

decoration goal
{
	orientation="ne";
	setorigin("ne",-20,0); // Смещение декорации для восьмиугольника
	
	if(hasproperty("rectanglenotation","0"))
	{
	}
	else
	{
		// Декарация «Цель» является полной копией
		ellipse(0,0, 100,100); // Окружности
		ellipse(15,15, 85,85);
		ellipse(30,30, 70,70);
		moveto(50,0); // Прицел — вертикаль верхняя
		lineto(50,40);
		moveto(50,60); // Прицел — вертикаль нижняя
		lineto(50,100);
		moveto(0,50); // Прицел — горизонталь левая
		lineto(40,50);
		moveto(60,50); // Прицел — горизонталь правая
		lineto(100,50);
		if(hastag("Погружение","Цель сопутствующая"))
		{
			ellipse(50,50, 100,100);
			ellipse(65,65, 85,85);
		}
	}
}

decoration composite
{
	orientation="se";
	if(hasproperty("iscomposite","true"))
	{
		// Три звена цепочки
		ellipse(0,40, 40,60);
		ellipse(60,40, 100,60);
		moveto(30,50);
		lineto(70,50);
	}
}