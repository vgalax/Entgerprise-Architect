shape main
{
	layouttype="border";
	noshadow=true;
	
	if(hasproperty("rectanglenotation","0"))
	{
		// Иконка
		defsize(90,45);
		// Первая капусла правее сверху
		startpath();
		moveto(35,90);
		lineto(85,90);
		arcto(60,-10, 110,90, 85,90, 85,-10);
		lineto(35,-10);
		arcto(10,-10, 60,90, 35,-10, 35,90);
		endpath();
		fillandstrokepath();
		// Вторая капусла левее снизу (базовая)
		startpath();
		moveto(25,100);
		lineto(75,100);
		arcto(50,0, 100,100, 75,100, 75,0);
		lineto(25,0);
		arcto(0,0, 50,100, 25,0, 25,100);
		endpath();
		fillandstrokepath();
	}
	else
	{
		// Прямоугольник
		defsize(100,70);
		roundrect(0,0, 100,100, 20,20); // Прямоугольник с закругленными углами

		addsubshape("padding","n"); // Создание отступа для декорации
		addsubshape("name","center");
		
		shape padding
		{
			preferredheight=20;
		}
		
		shape name
		{
			h_align="center";
			print("#name#");
		}
	}
}

shape label
{
	setorigin("sw",0,0);

	if(hasproperty("rectanglenotation","0"))
	{
		print("#name#");
	}
}

decoration superservice
{
	orientation="ne";
//	setorigin("ne", -25,5); // Смещение декорации для прямоугольника не выполняется!
	
	if(hasproperty("rectanglenotation","0"))
	{
	}
	else
	{
		// Внимание!!! Декорация «Суперсервиса» сжата в прямогоульнике и только две капуслы!!! Не чистое копирование
		// Верхняя капсула
		startpath();
		moveto(45,65);
		lineto(75,65);
		arcto(50,15, 100,65, 75,65, 75,15);
		lineto(45,15);
		arcto(20,15, 70,65, 45,15, 45,65);
		endpath();
		fillandstrokepath();
		// Нижняя капсула — X = -20, Y = +20
		startpath();
		moveto(25,85);
		lineto(55,85);
		arcto(30,35, 80,85, 55,85, 55,35);
		lineto(25,35);
		arcto(0,35, 55,85, 25,35, 25,85);
		endpath();
		fillandstrokepath();
	}
}

decoration composite
{
	orientation="se";
	if(hasproperty("iscomposite","true"))
	{
		// Три звена цепочки
		ellipse(0,40, 40,60);
		ellipse(60,40, 100,60);
		moveto(30,50);
		lineto(70,50);
	}
}